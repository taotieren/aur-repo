# Maintainer: taotieren <admin@taotieren.com>

pkgname=snow-git
_name=Snow
pkgver=0.10.0.r0.gd5161d2
pkgrel=1
_java=24
pkgdesc="AI-friendly programming language inspired by the LLM era. Its design goal is to make it easier for LLMs to generate and understand programming code."
arch=($CARCH)
url="https://gitee.com/jcnc-org/snow"
license=('Apache-2.0')
depends=(
    glibc
    zlib
)
makedepends=(
    jdk${_java}-graalvm-ee-bin
    'git'
    'maven'
    'musl'
)
optdepends=(
    "ideology: IDEology is the IDE for the Snow programming language."
)
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
replaces=()
backup=()
options=('!strip' '!debug')
#install=${pkgname}.install
source=("${pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}/"
    (
        set -o pipefail
        git describe --long --tag --abbrev=7 2>/dev/null | sed 's/^[vV]//g;s/\([^-]*-g\)/r\1/;s/-/./g' ||
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
    )
}

prepare() {
    git -C "${srcdir}/${pkgname}" clean -dfx
}

build() {
    cd "${pkgname}"
    mvn -P native-linux -DskipTests clean package
}

package() {
    cd "${pkgname}"
    install -Dm0755 target/${_name} "${pkgdir}/usr/bin/${pkgname%-git}"
    install -dm0775 "${pkgdir}/usr/lib/${pkgname%-git}" \
        "${pkgdir}/usr/share/doc/${pkgname%-git}"

    cp -R lib/* "${pkgdir}/usr/lib/${pkgname%-git}"
    cp -R docs/* "${pkgdir}/usr/share/doc/${pkgname%-git}"
    cp -R *.md "${pkgdir}/usr/share/doc/${pkgname%-git}"
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
