# Maintainer: taotieren <admin@taotieren.com>

pkgbase=hbdinetd-git
pkgname=hbdinetd-git
pkgver=r118.e7b1f42
pkgrel=12
pkgdesc="The daemon managing network interfaces for HybridOS."
arch=($CARCH)
url="https://github.com/HybridOS2/HBDInetd"
license=('GPL-3.0-only')
groups=(hybridos2-git)
provides=(${pkgbase%-git})
conflicts=(${pkgbase%-git})
replaces=()
depends=(
    hbdbus
    wpa_supplicant
    purc
    xguipro-git)
makedepends=(ccache
    cmake
    git
    ninja
    openssl
    pkgconf)
checkdepends=()
optdepends=()
source=("${pkgname%-git}::git+${url}.git")
sha256sums=('SKIP')
options=()

pkgver() {
    cd "${srcdir}/${pkgname%-git}/"
    #     git describe --long --tags | sed 's/ver.//g;s/\([^-]*-g\)/r\1/;s/-/./g'
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
    git -C "${srcdir}/${pkgname%-git}" clean -dfx
}

build() {
    export LDFLAGS="-L/lib64"
    if test -n "$LD_LIBRARY_PATH"; then
        export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:/lib64"
    else
        export LD_LIBRARY_PATH=/lib64
    fi
    cd "${srcdir}/${pkgname%-git}"

    sed -i 's|{HBDINETD_APP_INSTALL_DIR}/sbin/|{CMAKE_INSTALL_PREFIX}/bin|g' source/${pkgbase%-git}/CMakeLists.txt
    #    sed -i 's|{HBDINETD_APP_INSTALL_DIR}|{CMAKE_INSTALL_PREFIX}/\${HBDINETD_APP_INSTALL_DIR}|g' source/${pkgbase%-git}/PlatformLinux.cmake

    # seeï¼šhttps://wiki.archlinux.org/title/CMake_package_guidelines
    #     cmake -DCMAKE_BUILD_TYPE=Release \
    cmake -DCMAKE_BUILD_TYPE=None \
        -DPORT=Linux \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -Wno-dev \
        -B build \
        -G Ninja \
        -Wno-dev

    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C "${srcdir}"/${pkgname%-git}/build install
}
