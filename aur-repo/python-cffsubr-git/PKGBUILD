# Maintainer: taotieren <admin@taotieren.com>

pkgname=python-cffsubr-git
pkgver=0.2.9.post1.r2.g2a3b91f
pkgrel=1
pkgdesc='Standalone CFF subroutinizer based on AFDKO tx'
url='https://github.com/adobe-type-tools/cffsubr'
license=('Apache-2.0')
arch=(any)
depends=(
    afdko
    python
    python-fonttools)
makedepends=(
    python-setuptools-git-ls-files
    python-setuptools-scm
    python-{build,installer,wheel}
    git
    python-cmake-build-extension
    python-scikit-build
    cmake
    ninja)
checkdepends=(python-pytest)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("$pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    git describe --long --tags | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    git -C "${srcdir}/${pkgname}" clean -dfx
}

build() {
    cd "$pkgname"
    git submodule update --init --recursive
    python -m build -wn
}

# check() {
#     cd "$pkgname"
#     export PYTHONPATH="./src"
#     pytest tests
# }

package() {
    cd "$pkgname"
    python -m installer -d "$pkgdir" dist/*.whl
}
