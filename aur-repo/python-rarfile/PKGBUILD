# Maintainer: Bao Trinh <qubidt at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
pkgname=python-rarfile
_name=${pkgname#python-}
pkgver=4.2
pkgrel=2
pkgdesc="Python module for RAR archive reading"
arch=('any')
url="https://github.com/markokr/rarfile"
license=('ISC')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('unrar: decompression backend'
            'unarchiver: alternative decompression backend (unar)'
            'libarchive: alternative decompression backend (bsdtar)'
            'p7zip: alternative decompression backend (7z)'
            '7-zip: alternative decompression backend (7zz)'
            'python-cryptography: process archives with password-protected headers'
            'python-pycryptodome: alternative crypto backend')
checkdepends=('python-pytest'
              'unarchiver'
              'libarchive'
              'unrar'
              'p7zip'
              'python-cryptography'
              'python-pycryptodome')
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('dcf487b64541c07a47888d51fd199a96aae320e735a8ed923d698c4e6062f39f')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_name-$pkgver"
  pytest -v
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
