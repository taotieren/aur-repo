# Maintainer: taotieren <admin@taotieren.com>

pkgname=qucs-s-git
pkgver=r0.g1a3ce17
pkgrel=1
epoch=
pkgdesc="Qucs-S provides GUI for different circuit simulation kernels. "
arch=($CARCH)
url="https://github.com/ra3xdh/qucs_s"
license=('GPL-2.0-only')
_qt=qt5
groups=()
depends=(
    sh
    gcc-libs
    glibc
    hicolor-icon-theme
    python
    python-numpy
    python-matplotlib
    python-parse
    $_qt-base
    $_qt-charts
    $_qt-svg)
makedepends=(cmake
    ninja
    git
    autoconf
    automake
    perl-gd
    perl-xml-libxml
    gperf
    libtool
    flex
    bison
    $_qt-tools
)
optdepends=('ngspice: recommended simulation backend'
    'qucs: for Qucsator simulation backend'
    'freehdl: to permit digital circuit simulation'
    'asco: to enable circuit optimization')
checkdepends=()
optdepends=()
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
replaces=()
backup=()
options=('!makeflags')
install=
changelog=
source=("${pkgname}::git+${url}.git"
    "qucsator_rf::git+https://github.com/ra3xdh/qucsator_rf.git")
noextract=()
sha256sums=('SKIP'
    'SKIP')
validpgpkeys=()

pkgver() {
    cd "${srcdir}/${pkgname}"
    (
        set -o pipefail
        git describe --long --tag --abbrev=7 -match='[0-9]*' 2>/dev/null | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g' ||
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
    )
}

prepare() {
    git -C "${srcdir}/${pkgname}" clean -dfx
    cd "${srcdir}/${pkgname}"
    git submodule init
    git config submodule.qucsator_rf.url "$srcdir/qucsator_rf"
    git -c protocol.file.allow=always submodule update
}

build() {
    cd "${srcdir}/${pkgname}"

    cmake -D CMAKE_INSTALL_PREFIX=/usr \
        -B build \
        -G Ninja

    ninja -C build
}

package() {
    cd "${srcdir}/${pkgname}"
    DESTDIR="${pkgdir}" ninja -C "${srcdir}"/${pkgname}/build install
}
