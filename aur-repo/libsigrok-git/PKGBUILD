# Maintainer: David Manouchehri <manouchehri@riseup.net>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: veox <veox at wemakethings dot net>
# Contributor: Vuokko <vuokko@msn.com>

_gitname='libsigrok'
pkgname="${_gitname}-git"
pkgver=0.2.1.r4392.gf06f7881
pkgrel=3
pkgdesc="Client software that supports various hardware logic analyzers, core library (git version)"
arch=($CARCH)
url="http://www.sigrok.org/wiki/Libsigrok"
license=('GPL-3.0-only')
depends=(
  'glibc' 'glib2' 'gcc-libs' 'libzip' 'libftdi' 'libserialport-git'
  'glibmm' 'libieee1284' 'nettle' 'libtirpc' 'libsigc++' 'libusb'
  'hidapi' 'hicolor-icon-theme' 'python' 'zlib'
)
makedepends=('git' 'autoconf-archive' 'doxygen'
  'swig' 'python-setuptools' 'python-numpy' 'python-gobject' 'check'
)
conflicts=("${_gitname}")
provides=("${_gitname}")
source=(
  # "git://sigrok.org/${_gitname}"
  git+https://github.com/sigrokproject/libsigrok.git
)
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --exclude 'libsigrok-unreleased' --long | sed 's/^libsigrok-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  rm -rf "${srcdir}/build"
  mkdir -p "${srcdir}/build"
  cd "${srcdir}/${_gitname}"
  ./autogen.sh

  cd "${srcdir}/build"
  echo "CONFIGURE"
  ../${_gitname}/configure --prefix=/usr --disable-java --disable-ruby
  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" PREFIX=/usr install

  cd ../"${_gitname}"
  install -Dm 644 'contrib/60-libsigrok.rules' "${pkgdir}/usr/lib/udev/rules.d/60-libsigrok.rules"
  install -Dm 644 'contrib/61-libsigrok-uaccess.rules' "${pkgdir}/usr/lib/udev/rules.d/61-libsigrok-uaccess.rules"
}
