# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: taotieren <admin@taotieren.com>

_name=spsdk
pkgname=python-spsdk
pkgver=2.6.0
pkgrel=0
pkgdesc="NXP Secure Provisioning SDK"
arch=(any)
url="https://github.com/nxp-mcuxpresso/spsdk"
license=(BSD-3-Clause)
depends=(
  python
  python-asn1crypto
  python-astunparse
  python-bincopy
  python-bitstring
  python-click
  python-click-command-tree
  python-click-option-group
  python-colorama
  python-crcmod
  python-cryptography
  python-deepmerge
  python-fastjsonschema
  python-filelock
  python-hexdump
  python-importlib-metadata
  python-libusbsio
  python-libuuu
  python-oscrypto
  python-packaging
  python-platformdirs
  python-prettytable
  python-pyasn1
  python-pylink-square
  python-pyocd
  python-pyserial
  python-requests
  python-ruamel-yaml
  python-setuptools-scm
  python-sly
  #   python-spsdk-mcu-link
  #   python-spsdk-pyocd
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pyftdi
  python-pytest
  python-pytest-xdist
  python-voluptuous
)
optdepends=(
  'python-pyftdi: for dk6'
)
source=(
  $_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  #   $pkgname-2.1.0-remove_pypemicro.patch
)
sha512sums=('f9b10369dd14a90eb0c8c054d72382040fe796789c4be90831cc4d04eaa39c2682d5a6f814baaa436166b6dae80174dc8693b7ded1743368497d9daa1bf006af')

prepare() {
  cd $_name-$pkgver
  # remove all version pinning and overly explicit version bounds
  # https://github.com/NXPmicro/spsdk/issues/35
  sed -e 's|>=.*||g;s|==.*||g;s|~=.*||g;s|<.*||g' -i requirements{,-develop}.txt
  # remove dependency on python-pypemicro as it vendors prebuilt shared libraries
  # https://github.com/NXPmicro/spsdk/issues/30
  # https://github.com/NXPmicro/pypemicro/issues/10
  sed '/pypemicro/d' -i requirements.txt
  #   patch -Np1 -i ../$pkgname-2.1.0-remove_pypemicro.patch
  # remove dependency on python-pyocd-pemicro as it vendors prebuilt shared libraries via python-pypemicro
  # https://github.com/pyocd/pyOCD/issues/1319
  # https://github.com/NXPmicro/spsdk/issues/30
  # https://github.com/NXPmicro/pypemicro/issues/10
  sed '/pyocd-pemicro/d' -i requirements.txt
  sed -e '/spsdk-mcu-link/d' \
    -e '/spsdk-pyocd/d' \
    -i requirements.txt
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --ignore tests/mcu_examples/test_jupyter.py # we don't care about examples
    # tests due to missing files and whatever other reasons: https://github.com/nxp-mcuxpresso/spsdk/issues/66
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA1-secp256r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA1-secp384r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA1-secp521r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA1-rsa2048]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA1-rsa4096]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA256-secp384r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA256-secp521r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA384-secp256r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA384-secp521r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA384-rsa2048]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA384-rsa4096]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA512-secp256r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA512-secp384r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA512-rsa2048]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.SHA512-rsa4096]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.MD5-secp256r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.MD5-secp384r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.MD5-secp521r1]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.MD5-rsa2048]'
    --deselect 'tests/nxpcrypto/test_nxpcrypto.py::test_nxpcrypto_create_signature_algorithm[EnumHashAlgorithm.MD5-rsa4096]'
    --deselect 'tests/nxpimage/test_nxpimage_sb31.py::test_nxpimage_sb31_kaypair_not_matching'
  )

  cd $_name-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
