# Maintainer: taotieren <admin@taotieren.com>

pkgname=repo-misc-tools-git
pkgver=r88.f1192e6
pkgrel=3
pkgdesc="repo miscellaneous tools"
arch=(any)
url="https://github.com/taotieren/repo-misc-tools"
license=('MIT')
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
depends=('sh'
    #     'systemd-libs'
)
makedepends=('git')
source=("${pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    (
        set -o pipefail
        git describe --long --tag --abbrev=7 2>/dev/null | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g' ||
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
    )
}

prepare() {
    git -C "${srcdir}/${pkgname}" clean -dfx
}

package() {
    cd "${srcdir}/${pkgname}/cleanup-repo-tools/"
    install -Dm0755 cleanup-repo-tool.py "${pkgdir}/usr/bin/cleanup-repo-tool"
    install -Dm0644 -t "${pkgdir}/usr/lib/systemd/system" cleanup-repo-tool.service
    install -Dm0644 -t "${pkgdir}/usr/lib/systemd/system" cleanup-repo-tool.timer
    install -dm0755 "${pkgdir}/var/log/cleanup-repo-tool"
}
