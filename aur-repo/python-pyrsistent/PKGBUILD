# Maintainer: taotieren <admin@taotieren.com>
# Maintainer: Michał Wojdyła < micwoj9292 at gmail dot com >
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyrsistent
_name=${pkgname#python-}
pkgver=0.21.0
pkgrel=0
pkgdesc="Persistent/Immutable/Functional data structures"
arch=($CARCH)
license=('MIT')
url="https://github.com/tobgu/pyrsistent"
depends=(
  'glibc' 
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-pytest' 
  'python-hypothesis' 
  'python-typing_extensions'
)
source=("${pkgname}::git+https://github.com/tobgu/pyrsistent.git#tag=v${pkgver}")
sha512sums=('441e3980f730c60c54aeea076ac8d6c7f85e27f3bb7778c68aa8d066418f827b69dff42ca7a7c85687743ea6df89905c829945907fbc973f457d8755aa5300e1')

prepare() {
  git -C "${srcdir}/${pkgname}" clean -dfx
}

build() {
  cd "${srcdir}/${pkgname}"
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd "${srcdir}/${pkgname}"
  python -m installer --destdir=test_dir dist/*.whl
  PYTHONPATH="test_dir/$site_packages" pytest -vv
}
package() {
  cd "${srcdir}/${pkgname}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.mit -t "$pkgdir"/usr/share/licenses/$pkgname/
}
